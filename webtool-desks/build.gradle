buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.4.0.RELEASE'
        classpath 'gradle.plugin.org.kravemir.gradle.sass:GradleSassPlugin:1.2.1'
        classpath 'com.craigburke.gradle:client-dependencies:1.1.4'
    }
}

group 'org.techdraw.webtool.desks'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: "org.kravemir.gradle.sass"
apply plugin: 'com.craigburke.client-dependencies'


sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

clientDependencies {
    installDir = "$buildDir/assets/static"

    bower {
        'angular'('1.5.8')
        'ng-dialog'('0.6.2')
        'ngstorage'('0.3.11')
    }
}

sass {
    main {
        srcDir = file("$projectDir/src/main/sass")
        outDir = file("$buildDir/generated/resources")

        outSubDir = "static/css"
        registerInSourceSets ( 'main' )
    }
}

sourceSets {
    main {
        output.dir("$buildDir/assets")
    }
}

dependencies {
    compile project(':sheets')
    compile project(':desks')

    compile 'org.springframework.boot:spring-boot-starter-web:1.4.0.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-actuator:1.4.0.RELEASE'
    compile 'org.springframework.boot:spring-boot-devtools:1.4.0.RELEASE'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

springBoot {
    executable = true
}

jar {
    manifest {
        attributes(
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Main-Class': 'org.techdraw.webtool.desks.Application'
        )
    }
}